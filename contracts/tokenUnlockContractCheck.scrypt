import "util.scrypt";
import "tokenProto.scrypt";
import "rabinUtil.scrypt";

// opreturn + receiverTokenAmountArray + receiverArray + nReceivers(1 bytes) + tokenID
contract TokenUnlockContractCheck {
  int[3] rabinPubKeyArray;
    
  /**
   * @param changeSatoshis {int} change satoshis amount 
   * @param changeAddress {Ripemd160} change address 
  */
  public function unlock(
    SigHashPreimage txPreimage,
    int nSenders,
    bytes tokenScript,
    bytes prevouts,
    bytes rabinMsgArray,
    bytes rabinPaddingArray,
    bytes rabinSigArray,
    bytes inputTokenAddressArray,
    bytes inputTokenAmountArray,
    bytes receiverSatoshiArray,
    int nOutputs,
    bytes tokenOutputIndexArray,
    bytes otherOutputArray
  ) {

    require(Tx.checkPreimage(txPreimage));

    require(hash256(prevouts) == Util.hashPrevouts(txPreimage));

    bytes scriptCode = Util.scriptCode(txPreimage);
    int scriptLen = len(scriptCode);
    bytes tokenID = scriptCode[scriptLen - TokenProto.TOKEN_ID_LEN: ];

    tokenID = scriptCode[scriptLen - TokenProto.TOKEN_ID_LEN: scriptLen];
    int tokenScriptLen = len(tokenScript); 
    require(tokenID == TokenProto.getTokenID(tokenScript, tokenScriptLen));
    int pos = TokenProto.TOKEN_ID_LEN;

    int nReceivers = Util.fromLEUnsigned(scriptCode[scriptLen - pos - 1: scriptLen - pos]);
    pos += 1;

    bytes receiverArray = scriptCode[scriptLen - pos - nReceivers * TokenProto.TOKEN_ADDRESS_LEN: scriptLen - pos];
    pos += nReceivers * TokenProto.TOKEN_ADDRESS_LEN;

    bytes receiverTokenAmountArray = scriptCode[scriptLen - pos - nReceivers * TokenProto.TOKEN_AMOUNT_LEN: scriptLen - pos];

    int i = 0;
    int sumInputToken = 0;
    int paddingPos = 0;
    require(nSenders <= 8);
    // max support loop num input token
    loop(10) {
      if (i < nSenders) {
        int pubIndex = 0;
        // verify rabin signature
        bytes rabinMsg = rabinMsgArray[i * RabinUtil.U4_RABIN_MSG_LEN: (i + 1) * RabinUtil.U4_RABIN_MSG_LEN];
        int verifyCount = 0;
        loop(3) {
          int rabinPubKey = this.rabinPubKeyArray[pubIndex];
          int arrayIndex = i * 3 + pubIndex;
          int rabinSig = Util.fromLEUnsigned(rabinSigArray[arrayIndex * RabinUtil.RABIN_SIG_LEN: (arrayIndex + 1) * RabinUtil.RABIN_SIG_LEN]);
          int paddingLen = Util.fromLEUnsigned(rabinPaddingArray[paddingPos: paddingPos + RabinUtil.PADDING_SIZE_LEN]);
          bytes padding = b'';
          if (paddingLen > 0) {
            padding = rabinPaddingArray[paddingPos + RabinUtil.PADDING_SIZE_LEN: paddingPos + RabinUtil.PADDING_SIZE_LEN + paddingLen];
          }
          paddingPos += RabinUtil.PADDING_SIZE_LEN + paddingLen;
          int h = RabinUtil.rabinHash(rabinMsg + padding);
          if ((rabinSig * rabinSig) % rabinPubKey == h % rabinPubKey) {
            verifyCount += 1;
          }
          pubIndex += 1;
        }
        require(verifyCount >= 2);

        PayloadU4Data u4 = RabinUtil.loadPayloadU4(rabinMsg);
        require(u4.outpoint == prevouts[i * 36: (i + 1) * 36]);

        bytes address = inputTokenAddressArray[i * 20: (i + 1) * 20];
        int amount = Util.fromLEUnsigned(inputTokenAmountArray[i * 8: (i + 1) * 8]);
        bytes lockingScript = TokenProto.getNewTokenScript(tokenScript, tokenScriptLen, address, amount);

        require(hash160(lockingScript) == u4.scriptHash);

        sumInputToken = sumInputToken + amount;
      }
      i = i + 1;
    }
    
    // check the outputs
    bytes outputs = b'';
    // max support loop num receiver, you can change this num, but will cause the contrac size increase. you can customize your output
    i = 0;
    int sumOutputToken = 0;
    require(nOutputs < 10);
    int tokenIndex = 0;
    int otherOutputIndex = 0;
    loop(10) {
      if (i < nOutputs) {
        int tokenOutIndex = Util.fromLEUnsigned(tokenOutputIndexArray[tokenIndex * 4: (tokenIndex + 1) * 4]);
        if (i == tokenOutIndex) {
          tokenIndex += 1;
          bytes address = receiverArray[tokenIndex * TokenProto.TOKEN_ADDRESS_LEN: (tokenIndex + 1) * TokenProto.TOKEN_ADDRESS_LEN];
          int tokenAmount = Util.fromLEUnsigned(receiverTokenAmountArray[i * TokenProto.TOKEN_AMOUNT_LEN: (tokenIndex + 1) * TokenProto.TOKEN_AMOUNT_LEN]);
          sumOutputToken = sumOutputToken + tokenAmount;
          bytes outputScript = TokenProto.getNewTokenScript(tokenScript, tokenScriptLen, address, tokenAmount);
          int outputSatoshi = Util.fromLEUnsigned(receiverSatoshiArray[tokenIndex * 8: (tokenIndex + 1) * 8]);
          bytes output = Util.buildOutput(outputScript, outputSatoshi);
          outputs = outputs + output;
        } else {
          int outputLen = Util.fromLEUnsigned(otherOutputArray[otherOutputIndex: otherOutputIndex + 4]);
          bytes output = otherOutputArray[otherOutputIndex + 4: otherOutputIndex + 4 + outputLen];
          outputs += output;
          otherOutputIndex += 4 + outputLen;
          // check if other output is the same token output
          bytes scriptCode = TxUtil.getScriptCodeFromOutput(output);
          if (len(scriptCode) == tokenScriptLen) {
            require(TokenProto.getTokenID(scriptCode, tokenScriptLen) != tokenID);
          }
        }
      }
      i = i + 1;
    }
    require(sumInputToken == sumOutputToken);
    bytes hashOutputs = hash256(outputs);
    require(hashOutputs == Util.hashOutputs(txPreimage));
  }
}